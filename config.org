#+TITLE: Current Emacs (v30) config
#+AUTHOR: Daniel Elnatan
#+STARTUP: overview

* Preamble
[2025-11-07 Fri] this is fixed in the emacs-plus@30 build when I tried as of
today. So this block is commented out.

#+begin_src emacs-lisp
;; (use-package exec-path-from-shell
;;   :straight (exec-path-from-shell
;;              :type git :host github
;;              :repo "purcell/exec-path-from-shell")
;;   :init
;;   (when (memq window-system '(mac ns x))
;;     (exec-path-from-shell-initialize)))

;; toggle this to debug issues
;; (setq debug-on-error t)

;; use the latest transient (needed by magit, dirvish, and probably many others)
(use-package transient
  :straight t)
#+end_src


* Basic settings

#+begin_src emacs-lisp
;; keep things tidy
(use-package no-littering
  :straight t)

(set-language-environment "UTF-8")
(set-default-coding-systems 'utf-8)
(setq inhibit-startup-message t)
(setq custom-safe-themes t)
;; enable global auto revert (refresh buffer if there's file changes)
(global-auto-revert-mode 1)
(setq make-backup-files nil)
(setq auto-revert-verbose nil)
;; use y/n for yes-or-no
(setq use-short-answers t)
;; for programming set fill to 80 columns and display column indicator
(setq-default fill-column 79)
;; enable recent-file mode
(setq recentf-max-saved-items 50)
(recentf-mode 1)
;; refresh dired if file is changed on disk
(setq dired-auto-revert-buffer t)
;; enable side scrolling with mouse wheel
(setq mouse-wheel-tilt-scroll t)
;; reverse mouse direction for a more natural swipe feel
(setq mouse-wheel-flip-direction t)

;; start as a small frame
(setq initial-frame-alist
       '((top . 1) (left . 1) (width . 80) (height . 40)))

;; hooks to customize behavior of specific modes
(add-hook 'text-mode-hook 'visual-line-mode)
(add-hook 'prog-mode-hook 'visual-line-mode)
(add-hook 'prog-mode-hook #'display-fill-column-indicator-mode)

;; I only want to show line numbers in programming mode.
(add-hook 'prog-mode-hook 'display-line-numbers-mode)
(add-hook 'org-mode-hook 'visual-line-mode)

;; new in emacs 30, enable which-key-mode
(which-key-mode)
(column-number-mode)

;; Open recent files
(global-set-key (kbd "C-c r") 'recentf-open-files)

;; enable automatic parenthesis pairing
(electric-pair-mode 1)

;; default input mode to 'TeX', so can easily C-\ for inserting symbols
(setq default-input-method "TeX")

;; Kill current buffer (instead of asking first buffer name)
(global-set-key (kbd "C-x k") 'kill-current-buffer)

;; unbind annoying text-scaling with Ctrl-mouse wheel
(global-unset-key (kbd "C-<wheel-up>"))
(global-unset-key (kbd "C-<wheel-down>"))
(global-unset-key (kbd "C-<mouse-5>"))
(global-unset-key (kbd "C-<mouse-4>"))

;; Close frame if not the last, kill emacs else
(defun de/delete-frame-or-kill-emacs ()
  "Delete frame or kill Emacs if there is only one frame."
  (interactive)
  (if (> (length (frame-list)) 1)
      (delete-frame)
    (save-buffers-kill-terminal)))
(global-set-key (kbd "C-x C-c") 'de/delete-frame-or-kill-emacs)

;; create a new frame with *scratch* buffer
(defun de/new-frame ()
  "make a new frame with scratch buffer and switch to that buffer."
  (interactive)
  (select-frame (make-frame))
  (switch-to-buffer "*scratch*"))

(global-set-key (kbd "M-s-n") 'de/new-frame)
(global-set-key (kbd "M-`") 'other-frame)
#+end_src

In January 8, 2025, ~ultra-scroll~ came out and it seems to work well.
#+begin_src emacs-lisp
(use-package ultra-scroll
  :straight (ultra-scroll :type git :host github :repo "jdtsmith/ultra-scroll")
  :init
  (setq scroll-conservatively 101
    	scroll-margin 0)
  :config
  (ultra-scroll-mode 1))
#+end_src

Use multiple cursors
#+begin_src emacs-lisp
(use-package multiple-cursors
  :straight t
  :bind (("C->" . mc/mark-next-like-this)
         ("C-<" . mc/mark-previous-like-this)
    	 ("C-c C-<" . mc/mark-all-like-this)
         ("C-S-c C-S-c" . mc/edit-lines)
         ("C-S-<mouse-1>" . mc/add-cursor-on-click)))
#+end_src

Using the workflow from [[https://www.reddit.com/r/emacs/comments/910pga/tip_how_to_use_ivy_and_its_utilities_in_your/][TIP: How to use Ivy and its utilities in your workflow]]
(reddit r/emacs). I like using 'swiper' in place of the built-in isearch, so
the standard =C-s= and =C-r= keybinding is replaced here with its corresponding
version in swiper.

#+begin_src emacs-lisp
(use-package vertico
  :straight t
  :init (vertico-mode))

(use-package marginalia
  :straight t
  (:keymaps 'minibuffer-local-map
            "M-A" 'marginalia-cycle)
  :custom
  (marginalia-max-relative-age 0)
  (marginalia-align 'right)
  :init
  (marginalia-mode))

;; A few more useful configurations...
(use-package emacs
  :custom
  (indent-tabs-mode nil)
  (tab-width 4)
  (tab-always-indent 'complete)
  (text-mode-ispell-word-completion nil)
  (read-extended-command-predicate #'command-completion-default-include-p))

(use-package helpful
  :straight t
  :bind
  (("C-h f" . helpful-callable)
   ("C-h v" . helpful-variable)
   ("C-h k" . helpful-key)
   ("C-h x" . helpful-command)
   ("C-h o" . helpful-symbol)
   ;; Lookup the current symbol at point
   ("C-c C-d" . helpful-at-point)))

(use-package orderless
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles partial-completion))))
  (completion-category-defaults nil))

(use-package consult
  :bind (("C-x b" . consult-buffer)
         ("C-s" . consult-line)
         ("C-r" . consult-line)))
  #+end_src


** Enhancements

The default keybinding to switch to another window is =C-x o=, let's just make this shorter since I use it often. This is being re-bound to ~ace-window~.
#+begin_src emacs-lisp
;; (global-set-key (kbd "M-o") 'other-window)
(use-package ace-window
  :straight t
  :bind
  (("M-o" . ace-window)))
#+end_src

Switching to using ~dirvish~ for an enhanced dired experience
#+begin_src emacs-lisp
(use-package dirvish
  :straight t
  :init
  ;; Enable dirvish globally (replaces standard dired)
  (dirvish-override-dired-mode)
  :custom
  ;; Go back to the 'vscode-icon' style you had, or use 'nerd-icons'
  (dirvish-attributes '(nerd-icons file-time file-size collapse subtree-state vc-state git-msg))
  (dirvish-side-width 30)
  :config
  ;; Enable "follow mode" so the sidebar updates as you switch files
  (dirvish-side-follow-mode)
  :bind
  ;; Bind a key to toggle the sidebar
  ("C-x C-n" . dirvish-side) ;; 'o' for outline/open
  :hook
  ;; Clean up the sidebar look
  (dirvish-side-mode . (lambda ()
                         (display-line-numbers-mode -1)
                         (visual-line-mode -1)
                         (hl-line-mode 1))))
#+end_src

I'd like to be able to toggle horizontal/vertical split when I have 2 windows
open. Found in [[https://emacs.stackexchange.com/questions/318/switch-window-split-orientation-fastest-way][Emacs Stackexchange]].

#+begin_src emacs-lisp
(defun de/toggle-split-direction ()
  (interactive)
  (if (= (count-windows) 2)
      (let* ((this-win-buffer (window-buffer))
             (next-win-buffer (window-buffer (next-window)))
             (this-win-edges (window-edges (selected-window)))
             (next-win-edges (window-edges (next-window)))
             (this-win-2nd (not (and (<= (car this-win-edges)
                                         (car next-win-edges))
                                     (<= (cadr this-win-edges)
                                         (cadr next-win-edges)))))
             (splitter
              (if (= (car this-win-edges)
                     (car (window-edges (next-window))))
                  'split-window-horizontally
                'split-window-vertically)))
        (delete-other-windows)
        (let ((first-win (selected-window)))
          (funcall splitter)
          (if this-win-2nd (other-window 1))
          (set-window-buffer (selected-window) this-win-buffer)
          (set-window-buffer (next-window) next-win-buffer)
          (select-window first-win)
          (if this-win-2nd (other-window 1))))))

(global-set-key (kbd "C-x |")  'de/toggle-split-direction)

#+end_src

I often use fill-region or org-fill-paragraph when not using visual-line-mode,
but there's no native function that would do the inverse of these
operations. The unfill package does just that.

#+begin_src emacs-lisp
(use-package unfill
  :straight t)
#+end_src


** Navigation

I seldom use =C-v= or =M-v= to move page-by-page. So here I rebind these
keybindings to custom functions that scrolls half-page up/down & keep things in
the center for easier viewing:
#+begin_src emacs-lisp
(defun de/scroll-half-page-down ()
  (interactive)
  (move-to-window-line-top-bottom)
  (move-to-window-line-top-bottom)
  (recenter-top-bottom))

(defun de/scroll-half-page-up ()
  (interactive)
  (move-to-window-line-top-bottom)
  (recenter-top-bottom)
  (recenter-top-bottom))

(global-set-key (kbd "C-v") 'de/scroll-half-page-down)
(global-set-key (kbd "M-v") 'de/scroll-half-page-up)
#+end_src


* Programming setup
** General setup

Using magit for git project management.

#+begin_src emacs-lisp :tangle yes
(use-package magit
  :defer t
  :straight t
  :bind (("C-x g" . magit-status)))
#+end_src

Setup ~treesitter~ for several languages. To tell whether the current buffer is
using the ~ts~ mode is by running =M-x major-mode=.

#+begin_src emacs-lisp
(add-to-list 'treesit-language-source-alist
  	         '(python . ("https://github.com/tree-sitter/tree-sitter-python"))
             '(cpp    . ("https://github.com/tree-sitter/tree-sitter-cpp"))
             '(c      . ("https://github.com/tree-sitter/tree-sitter-c")))
(add-to-list 'major-mode-remap-alist '(c-mode . c-ts-mode))
(add-to-list 'auto-mode-alist '("\\.c\\'" . c-ts-mode))
#+end_src

Silence eglot progress (in the *Messages* buffer)
#+begin_src emacs-lisp
(setq eglot-report-progress nil)
#+end_src

I have ~emacs-lsp-booster~ installed and setup in my ~PATH~, so I'd like to speed up LSP via ~eglot-booster~.

Since eglot is the LSP client, you can also configure each LSP settings 'globally' by defining the variables in ~eglot-workspace-configuration~ variable.

#+begin_src emacs-lisp
(use-package eglot
  :defer t
  :straight (:type built-in)
  :bind
  (:map eglot-mode-map
        ("C-c C-d" . eldoc)
        ("C-c C-f" . eglot-format-buffer))
  :hook ((python-base-mode . eglot-ensure)
         (rust-mode . eglot-ensure))
  :custom
  (eglot-autoshutdown t))

(use-package eglot-booster
  :straight (eglot-booster :type git :host github :repo "jdtsmith/eglot-booster")
  :after eglot
  :config (eglot-booster-mode))
#+end_src

Use ~avy~ via keybinding =M-g= as a prefix for avy. where 'c' is go to char, 't'
uses a timer to type some characters, and 'l' for going to a specific line.
#+begin_src emacs-lisp
(use-package avy
  :straight t)

;; configure avy globally, use prefix M-g 
(global-set-key (kbd "M-g c") 'avy-goto-char)
(global-set-key (kbd "M-g t") 'avy-goto-char-timer)
(global-set-key (kbd "M-g l") 'avy-goto-line)
#+end_src

Use `treesit-fold` for code folding
#+begin_src emacs-lisp
(use-package treesit-fold
  :straight (treesit-fold :type git :host github :repo "emacs-tree-sitter/treesit-fold")
  :hook (prog-mode . treesit-fold-mode)
  :bind
  (:map prog-mode-map
        ("C-c f f" . treesit-fold-toggle)    
        ("C-c f c" . treesit-fold-close)     
        ("C-c f o" . treesit-fold-open)      
        ("C-c f r" . treesit-fold-open-recursively)
        ("C-c f C" . treesit-fold-close-all)  
        ("C-c f O" . treesit-fold-open-all))) 

(use-package treesit-fold-indicators
  :straight (treesit-fold-indicators :type git :host github :repo "emacs-tree-sitter/treesit-fold")
  :config
  (global-treesit-fold-indicators-mode))
#+end_src

For general code formatting I use ~apheleia~. Python code formatting uses =ruff=
installed via homebrew. Doing so will obviate installing a formatter for every
Python environment.

For ~rustfmt~ and ~prettier~ you need to install those with homebrew.

#+begin_src emacs-lisp
(use-package apheleia
  :straight t
  :config
  ;; supress auto-revert warnings
  (setq apheleia-inhibit-functions
    	(cons #'buffer-modified-p apheleia-inhibit-functions))
  ;; define formatters
  (setf (alist-get 'ruff apheleia-formatters)
        '("ruff" "format" "--verbose" "--line-length" "79"
    	  "--stdin-filename" filepath))
  (setf (alist-get 'prettier-typescript apheleia-formatters)
    	'("prettier" "--use-tabs=false" "--print-width" "80"
    	  "--stdin-filename" filepath "--parser=typescript"))
  (setf (alist-get 'prettier-json apheleia-formatters)
    	'("prettier" "--use-tabs=false" "--print-width" "80"
    	  "--stdin-filename" filepath "--parser=json"))
  (setf (alist-get 'prettier-javascript apheleia-formatters)
    	'("prettier" "--use-tabs=false" "--print-width" "80"
    	  "--stdin-filename" filepath "--parser=babel-flow"))
  ;; define commands for each mode
  (setf (alist-get 'python-ts-mode apheleia-mode-alist) '(ruff-isort ruff))
  (setf (alist-get 'tsx-ts-mode apheleia-mode-alist) 'prettier-typescript)
  (setf (alist-get 'js-ts-mode apheleia-mode-alist) 'prettier-javascript)
  :hook (prog-mode . apheleia-mode))

(require 'apheleia)
#+end_src

Use ~corfu~ for autocompletion. You can use multiple words to filter your search
by using a separator, which is bound to the key =M-<space>= when a pop-up box is
on the screen. Sometimes the partial match can get in the way of doing things,
like choosing to rename your file to something else that is a sub/superset of
the string. Do =M-<enter>= to enter the literal entry, rather than the match.

#+begin_src emacs-lisp
(use-package corfu
  :after orderless
  :straight t
  :custom
  (corfu-auto t)              ; enable auto-completion
  (corfu-cycle t)             ; allow cycling through candidates
  (corfu-separator ?\s)       ; orderless field separator
  (corfu-auto-prefix 2)       ; trigger after 2 chars
  (corfu-auto-delay 0.1)      ; short delay for IDE-like feel
  (corfu-popupinfo-delay 0.4) ; delay before doc popup appears
  (corfu-quit-at-boundary nil)
  :init
  (global-corfu-mode)
  (corfu-popupinfo-mode)
  :bind
  (:map corfu-map
        ("M-SPC" . corfu-insert-separator)
        ("M-n" . corfu-popupinfo-scroll-up)
        ("M-p" . corfu-popupinfo-scroll-down)))

(use-package nerd-icons-corfu
  :straight t
  :after corfu
  :config
  (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))

;; add corfu extension
(use-package cape
  :straight t
  :bind (("C-c p p" . completion-at-point) ; explicitly trigger completion
         ("C-c p t" . cape-tex)
         ("C-c p f" . cape-file)
         ("C-c p d" . cape-dabbrev)
         ("C-c p s" . cape-elisp-symbol)
         ("C-c p e" . cape-elisp-block))
  :init
  ;; use cape-file everywhere
  (add-to-list 'completion-at-point-functions #'cape-file)
  ;; only check for elisp blocks inside org files
  (add-hook
   'org-mode-hook (lambda ()
                    (add-to-list 'completion-at-point-functions #'cape-elisp-block)))
  (add-hook
   'latex-mode-hook (lambda ()
                      (add-to-list 'completion-at-point-functions #'cape-tex))))
#+end_src

A note for using ~corfu~:
=M-h= is bound to ~corfu-popupinfo-documentation~ which show/hide the docstring popup.


** Python

Simple Python setup using venv for managing environments. I've been using `uv` to manage my Python environments and `venv` for creating separate environments. You can simply run:

~uv venv my-new-env --python 3.11~ to create a new environment.

#+begin_src emacs-lisp
(use-package pythonic)

;; load my custom venv manager
;; which provides activate-venv & deactivate-venv
(use-package venv-manager
  :straight nil
  :load-path "/Users/delnatan/Apps/emacs-config/custom/venv-manager"
  :init
  (require 'venv-manager)
  :custom
  (venv-manager-directories '("/Users/delnatan/envs")))

;; set the default python to 'utils' (remember to create this!)
(activate-venv "utils")
  #+end_src

#+begin_src emacs-lisp
(use-package python
  :config
  ;; disable the new Python 3.13+ fancy REPL which breaks emacs DUMB terminals
  (add-to-list 'python-shell-process-environment "PYTHON_BASIC_REPL=1")
  (setq python-indent-guess-indent-offset t)
  (setq python-indent-guess-indent-offset-verbose nil)
  ;; use tree-sitter
  (when (treesit-ready-p 'python)
    (setq major-mode-remap-alist
  	      (append '((python-mode . python-ts-mode)) major-mode-remap-alist))))

#+end_src

#+begin_src emacs-lisp
(defun de/restart-python ()
  "Clear current inferior python buffer and restart process"
  (interactive)
  (progn (with-current-buffer "*Python*" (comint-clear-buffer))
  	     (python-shell-restart)))

;; custom function to kill current cell
(defun de/kill-cell ()
  "code-cells mode custom function to kill current cell"
  (interactive)
  (let ((beg (car (code-cells--bounds)))
  	    (end (cadr (code-cells--bounds))))
    (kill-region beg end)))

(use-package code-cells
  :straight t
  :defer t
  :hook ((python-ts-mode . code-cells-mode-maybe)
  	     (python-mode . code-cells-mode-maybe))
  :config
  (add-to-list 'code-cells-eval-region-commands
               '(python-ts-mode . python-shell-send-region) t)
  :bind
  (:map
   code-cells-mode-map
   ("M-p" . code-cells-backward-cell)
   ("M-n" . code-cells-forward-cell)
   ("C-c r p" . de/restart-python)
   ("C-c d d" . de/kill-cell)
   ("M-S-<up>" . code-cells-move-cell-up)
   ("M-S-<down>" . code-cells-move-cell-down)
   ("C-c x ;" . code-cells-comment-or-uncomment)
   ("C-c C-c" . code-cells-eval)))
#+end_src

** Jupyter setup

I'm using my own fork of the jupyter package because I don't like the default
behavior of the jupyter-display buffer when using a jupyter kernel. A new frame
would be made, rather than being re-used, if the window associated with the
kernel does not belong to the same frame. This became annoying when I use a
second monitor to have a separate frame for displaying plots and text output
from the jupyter kernel.

#+begin_src emacs-lisp
(use-package jupyter
  :straight (jupyter :type git :local-repo "/Users/delnatan/Apps/emacs/jupyter/")
  :defer t
  :custom
  (jupyter-eval-use-overlays t)
  ;; (jupyter-repl-echo-eval-p t)
  (jupyter-repl-prompt-margin-width 5)
  :config
  (add-to-list 'display-buffer-alist
	           '("\\*jupyter-output\\*"
		         (display-buffer-reuse-window
		          display-buffer-pop-up-window)
		         (reusable-frames . t))))

;; for using gnuplot
(use-package gnuplot
  :defer t
  :straight t
  :config
  (add-hook 'gnuplot-mode-hook
            (lambda ()
              (set (make-local-variable 'compile-command)
                   (concat "gnuplot " buffer-file-name)))))

;; enable languages for org-babel
(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)
   (awk . t)
   (sed . t)
   (sqlite . t)
   (shell . t)
   (gnuplot . t)
   (python . t)
   (jupyter . t)))

;; uncomment below to override `python` language designation
;; use `jupyter-python` for jupyter and `python` for vanilla python
;; (org-babel-jupyter-override-src-block "python")

;; patch for correct handling of 'python' org source blocks
(add-to-list 'org-src-lang-modes '("python" . python-ts))

;; there seems to be already 'jupyter-python' entry in the list
;; so we remove that, then add our own with 'python-ts'
(setq org-src-lang-modes
      (cons '("jupyter-python" . python-ts)
            (assq-delete-all "jupyter-python" org-src-lang-modes)))
#+end_src

A typical workflow in org-mode is to use source blocks with the following tag (after running =activate-venv=!):
#+begin_example
  #+PROPERTY: header-args:python :session py
  #+PROPERTY: header-args:python+ :async yes
  #+PROPERTY: header-args:python+ :kernel GEManalysis
  #+begin_src python :session py :kernel GEManalysis :async yes
  <python code goes here>
  #+end_src

#+end_example

To make life a bit simpler, I've made a function to insert this snippet with the help of ChatGPT. To insert the snippet above in an org file, do =C-c j=. The ~never-export~ option tells org not to re-evaluate the entire document whenever the document is exported.
#+begin_src emacs-lisp
(defun de/insert-org-jupyter-kernel-spec ()
  "Interactively insert a Jupyter kernel spec at the beginning of an Org document.
      Ensure 'jupyter' is available, or interactively activate it using 'micromamba-activate'."
  (interactive)
  (unless (executable-find "jupyter")
    (call-interactively 'micromamba-activate)) ;; Call `micromamba-activate` interactively to ensure prompt.
  ;; Ensure 'jupyter' is available after activation attempt.
  (if (executable-find "jupyter")
      (let* ((kernelspec (jupyter-completing-read-kernelspec))
             (kernel-name (jupyter-kernelspec-name kernelspec))
             (kernel-display-name
              (plist-get (jupyter-kernelspec-plist kernelspec) :display_name))
             (insertion-point (point-min))
             (properties
              (format
               "#+PROPERTY: header-args:jupyter-python :session py
  ,#+PROPERTY: header-args:jupyter-python+ :async yes
  ,#+PROPERTY: header-args:jupyter-python+ :eval never-export
  ,#+PROPERTY: header-args:jupyter-python+ :kernel %s\n"  kernel-name)))
        (save-excursion
          (goto-char insertion-point)
          (insert properties)
          (message "Inserted Jupyter kernel spec for '%s'." kernel-display-name)))
    (message "Jupyter is not available. Please ensure it is installed and try again.")))

(defun de/org-jupyter-setup ()
  (define-key org-mode-map (kbd "C-c j") 'de/insert-org-jupyter-kernel-spec))

(add-hook 'org-mode-hook 'de/org-jupyter-setup())
#+end_src

You can navigate between org-mode blocks with keybindings =C-c C-v n/p= for
next/previous blocks.

As of [2024-03-29 Fri], ansi colors in the org-mode results is not rendering
correctly. This is a workaround found in the ~emacs-jupyter~ issues list:
#+begin_src emacs-lisp
(defun patch/display-ansi-colors ()
  "Fixes kernel output in emacs-jupyter"
  (ansi-color-apply-on-region (point-min) (point-max)))
(add-hook 'org-mode-hook
          (lambda ()
            (add-hook 'org-babel-after-execute-hook #'patch/display-ansi-colors)))
#+end_src

For prototyping a lot of code, I typically open a Python file and associate a
jupyter console to it. Since I do this a lot, I decided to simplify this into a
function:

#+begin_src emacs-lisp
(defun de/python-with-jupyter-repl
    (kernel-name &optional repl-name filename)
  "Choose jupyter kernel to open/start new Python file associated to it.
  With prefix argument (C-u), use the current buffer if it's a Python file."
  (interactive
   (let ((current-file (and (memq major-mode '(python-mode python-ts-mode))
                            (buffer-file-name))))
     (if current-prefix-arg
         (if current-file
             (list
              (jupyter-kernelspec-name
               (jupyter-completing-read-kernelspec nil current-prefix-arg))
              (let ((input (read-string "REPL name (empty for default): " nil nil nil)))
                (if (string-empty-p input)
                    (file-name-base current-file)
                  input))
              current-file)
           (error "Current buffer is not a Python file"))
       ;; Original behavior when no prefix
       (let ((file (read-file-name "Open Python file: " nil nil nil)))
         (list
          (jupyter-kernelspec-name
           (jupyter-completing-read-kernelspec nil current-prefix-arg))
          (if current-prefix-arg
              (read-string "REPL name: ")
            (file-name-base file))
          file)))))

  ;; open or create the Python file if needed
  (unless (equal (buffer-file-name) filename)
    (find-file filename))

  ;; start the jupyter REPL and store the client symbol
  (let ((client (jupyter-run-repl kernel-name repl-name)))
    ;; wait for REPL to start and then associate the buffer
    (sleep-for 1.0)
    (jupyter-repl-associate-buffer client)))

#+end_src

Convenience function to insert matplotlib plots in svg rather than the basic PNG.
#+begin_src emacs-lisp
;; place cursor within the top of the python source block
(defun de/insert-inline-svg-matplotlib ()
  (interactive)
  (insert "import matplotlib_inline\n")
  (insert "matplotlib_inline.backend_inline.set_matplotlib_formats(\"svg\")"))
#+end_src


* Theme and appearance

I really like Nicolas Rougier's nano-modeline, so we'll set that up independent
of whatever theme I end up using.

The ~(&rest _)~ trick is used here to ignore any arguments that is passed to the
function (it can be called with any number of arguments, but they will be
ignored). ~enable-theme-functions~ calls its functions with one argument. My
function works without using any, so I will just ignore the argument.

#+begin_src emacs-lisp
(use-package nano-modeline
  :straight (nano-modeline :type git :host github :repo "rougier/nano-modeline")
  :config
  ;; enable modeline globally
  (nano-modeline-text-mode t))

(setq-default mode-line-format nil)
#+end_src

** Nicolas Rougier's NANO emacs
Use Nicolas Rougier's ~nano-emacs~. For fonts (on MacOS), I install them
using ~homebrew~.  

#+begin_src emacs-lisp
(use-package nano-theme
  :straight (nano-theme :type git :local-repo "/Users/delnatan/Apps/emacs/nano-theme/")
  :init
  ; Use your own fonts, NOT NANO fonts
  (setq nano-fonts-use nil)  
  :config
  ;; define a function to set custom faces
  (defun de/nano-theme-overrides (&rest _)
    "Apply custom font and face overrides for nano-theme."
    ;; -- Global Font Settings --
    (set-face-attribute 'default nil 
                        :family "Roboto Mono"
                        :height 130
                        :weight 'light)
    (set-face-attribute 'italic nil
                        :family "Victor Mono"
                        :slant 'italic
                        :height 130)
    (set-face-attribute 'fixed-pitch nil
                        :family "Roboto Mono"
                        :height 130
                        :weight 'light)
    (set-face-attribute 'variable-pitch nil
                        :family "Roboto Slab"
                        :height 135)
    
    ;; nano-modeline theme-switching fix
    (when (featurep 'nano-modeline)
      (set-face-attribute 'nano-modeline-active nil
                          :background 'unspecified
                          :foreground 'unspecified
                          :inherit 'nano-subtle)
      (set-face-attribute 'nano-modeline-inactive nil
                          :background 'unspecified
                          :foreground 'unspecified
                          :inherit 'nano-faded)
      (force-mode-line-update t))
    
    ;; org mode customization
    (when (featurep 'org) 
      ;; Keep highlight background but use fixed-pitch font
      (set-face-attribute 'org-block nil :inherit '(highlight fixed-pitch))
      (set-face-attribute 'org-code nil :inherit '(nano-salient fixed-pitch))
      (set-face-attribute 'org-block-begin-line nil :inherit '(nano-faded fixed-pitch))
      (set-face-attribute 'org-block-end-line nil :inherit '(nano-faded fixed-pitch))
      (set-face-attribute 'org-verbatim nil :inherit '(nano-popout fixed-pitch))
      (set-face-attribute 'org-table nil :inherit '(nano-faded fixed-pitch))
      ;; keep org-hide hidden when switching theme
      (set-face-attribute 'org-hide nil :foreground (face-attribute 'default :background))
      (set-face-attribute 'org-checkbox nil :inherit '(nano-faded fixed-pitch))
      (set-face-attribute 'org-special-keyword nil :inherit '(nano-faded fixed-pitch))
      (set-face-attribute 'org-meta-line nil :inherit '(nano-faded fixed-pitch))
      (set-face-attribute 'corfu-default nil :inherit '(nano-subtle fixed-pitch))
      
      ;; Headings with variable-pitch, keeping nano-strong styling
      (set-face-attribute 'org-document-title nil :inherit '(nano-strong variable-pitch) :height 1.4)
      (set-face-attribute 'org-level-1 nil :inherit '(nano-strong variable-pitch) :height 1.2)
      (set-face-attribute 'org-level-2 nil :inherit '(nano-strong variable-pitch) :height 1.1)
      (set-face-attribute 'org-level-3 nil :inherit '(nano-strong variable-pitch) :height 1.05)
      (set-face-attribute 'org-level-4 nil :inherit '(nano-strong variable-pitch) :height 1.0)
      (set-face-attribute 'org-level-5 nil :inherit '(nano-strong variable-pitch) :height 1.0)))

  (advice-add 'nano-light :after #'de/nano-theme-overrides)
  (advice-add 'nano-dark :after #'de/nano-theme-overrides)
  
  (with-eval-after-load 'org (de/nano-theme-overrides))
  (with-eval-after-load 'nano-modeline (de/nano-theme-overrides))

  ;; initial setup
  (nano-mode)
  ;; modify popout color
  (set-face-attribute 'nano-popout nil :foreground "#D0634A")
  ;; reset this again just to be safe
  (setq nano-fonts-use nil)
  ;; load initial theme as light
  (nano-light))

                      #+end_src

I want to show the colors of hex codes in the buffer so I'm using ~rainbow-mode~.
#+begin_src emacs-lisp
(use-package rainbow-mode
  :straight t
  :hook (org-mode prog-mode))
#+end_src


* Rougier's wonderful tools

#+begin_src emacs-lisp
;; add emacs ~app~ folder to load-path
(add-to-list 'load-path "~/Apps/emacs/notes-list")  
(add-to-list 'load-path "~/Apps/emacs/svg-tag-mode")
(add-to-list 'load-path "~/Apps/emacs/nano-tools")

(require 'nano-writer)
;; use Rougier's writer mode for a nicer org-mode

(use-package svg-lib
  :defer t
  :straight (svg-lib :type git :host github :repo "rougier/svg-lib"))

(use-package stripes
  :defer t
  :straight t)

(require 'notes-list)

(defun de/insert-org-note-properties ()
  "Insert common Org properties at the beginning of the document."
  (interactive)
  (let ((title (read-string "Title: "))
        (filetags (read-string "File tags: "))
        (summary (read-string "Summary: "))
        (date (format-time-string "%Y-%m-%d"))
        (icon "material/notebook"))
    (goto-char (point-min))
    (insert (format "#+TITLE: %s\n" title))
    (insert (format "#+DATE: %s\n" date))
    (insert (format "#+FILETAGS: %s\n" filetags))
    (insert (format "#+SUMMARY: %s\n" summary))
    (insert (format "#+ICON: %s\n" icon))))

(with-eval-after-load 'org
  (define-key org-mode-map (kbd "C-c i p") 'de/insert-org-note-properties))

#+end_src

** buffer-box for borders around frames

#+begin_src emacs-lisp
(use-package buffer-box
  :straight nil
  :load-path "/Users/delnatan/Apps/emacs/buffer-box/"
  :bind
  ("C-c b" . buffer-box)
  ;; :hook
  ;; (after-change-major-mode . buffer-box)
  )
#+end_src


* org-mode configuration
:PROPERTIES:
:header-args: :tangle yes
:END:
Minor reconfiguration of ~org-mode~.

#+begin_src emacs-lisp
(use-package org
  :config
  (add-hook 'org-mode-hook 'org-indent-mode)
  (setq org-startup-indented t)
  (set-face-attribute 'org-indent nil :inherit '(org-hide fixed-pitch))
  (setq org-confirm-babel-evaluate nil)
  (setq org-display-inline-images t)
  (setq org-adapt-indentation t)
  (setq org-hide-leading-stars t)
  (setq org-src-fontify-natively t)
  (setq org-src-tab-acts-natively t)
  (setq org-edit-src-content-indentation 0)
  (setq org-startup-with-inline-images t)
  (setq org-src-window-setup 'split-window-below)
  (setq org-format-latex-options
	    (plist-put org-format-latex-options :scale 1.1))
  (setq org-image-actual-width nil)
  (setq org-export-default-inline-image-rule
	    '(("file" . "\\.\\(gif\\|jp\\(?:e?g\\)\\|svg?\\|p\\(?:bm\\|gm\\|ng\\|pm\\)\\|tiff?\\|x\\(?:[bp]m\\)\\'")))
  (setq org-src-preserve-indentation t)
  (setq org-latex-compiler "xelatex")
  (setq org-latex-pdf-process
	    (list "latexmk -f -pdf -%latex -shell-escape -interaction=nonstopmode -output-directory=%o %f"))
  :bind (:map org-mode-map
	          ("C-c l" . org-store-link)
	          ("C-c C-l" . org-insert-link)
	          ("C-x v l" . org-toggle-link-display)))

;; Enable variable-pitch-mode in org buffers
(add-hook 'org-mode-hook 'variable-pitch-mode)

;; remove under/over line in org source block header/footer
(add-hook 'org-babel-after-execute-hook 'org-redisplay-inline-images)

;; shortcut to insert source block
(add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
(add-to-list 'org-structure-template-alist '("sp" . "src python"))
(add-to-list 'org-structure-template-alist '("sjp" . "src jupyter-python"))

;; use engrave-faces to support fontifying source blocks in LaTeX exports
(use-package engrave-faces
  :straight t
  :defer t
  :init
  (setq org-latex-src-block-backend 'engraved))

(setq org-latex-engraved-theme 'nano)
#+end_src

For a nicer org source block experience, trying ~corg.el~. You can trigger
completion at point to see what's available. Typically I prefer just invoking
~complete-symbol~ via `M-<tab>`.

#+begin_src emacs-lisp
(use-package corg
  :straight (corg :host github :repo "isamert/corg.el")
  :hook (org-mode . corg-setup))
#+end_src

Setup a more modern-looking org-mode and use ~org-fragtog~ for nicer experience
writing LaTeX in org documents.

#+begin_src emacs-lisp
(use-package org-fragtog
  :straight t
  :hook (org-mode . org-fragtog-mode))

;; makes editing emphasized text easier
(use-package org-appear
  :commands (org-appear-mode)
  :hook     (org-mode . org-appear-mode)
  :config
  (setq org-hide-emphasis-markers t)  ; Must be activated for org-appear to work
  (setq org-appear-autoemphasis   t   ; Show bold, italics, verbatim, etc.
        org-appear-autolinks      t   ; Show links
		org-appear-autosubmarkers t)) ; Show sub- and superscripts
#+end_src

Let's change the bullets appearance too.
#+begin_src emacs-lisp
(use-package org-superstar
  :straight t
  :after org
  :hook (org-mode . org-superstar-mode)
  :config
  ;; Hide the leading asterisks (e.g., "** Headline" becomes "   Headline")
  ;; This creates a much cleaner outline structure.
  (setq org-superstar-remove-leading-stars t)

  ;; Set specific bullets for the first few levels.
  ;; This list cycles if you go deeper than the defined length.
  ;; Options: ◉ ○ ✸ ✿ ⁖ ⊚ ⊛ ∙ ▷
  (setq org-superstar-headline-bullets-list '("◉" "○" "⚬" "◈" "◇" "✳" "◎"))

  ;; Optional: If you want to prettify plain lists ( - or + ) as well
  (setq org-superstar-item-bullet-alist
        '((?* . ?•)
          (?+ . ?➤)
          (?- . ?–))))
#+end_src

A neat trick for when writing LaTeX snippets is the =C-c C-x C-l= keybinding to show/hide preview of latex. You enclose the expression with =\[= and =\]= or =$=.

For some reason, I'm having trouble (specifically on MacOS) rendering LaTeX fragments within an org document whenever the org file is in any of my Dropbox folder. So here I'm trying to see if changing the temporary directory to be in an absolute local folder instead of a relative one helps. This didn't solve the problem! But going straight to the directory at =~/Library/CloudStorage/Dropbox= doesn't!

#+begin_src emacs-lisp
(setq org-latex-preview-image-directory (expand-file-name "~/.emacs.d/tmp"))
(setq org-latex-preview-ltxpng-directory (expand-file-name "~/.emacs.d/tmp"))
(setq temporary-file-directory (file-truename "~/.emacs.d/tmp"))
#+end_src

To preview images =C-c C-x C-v= or invoke =org-toggle-inline-images=. Images are inserted like regular links, just enclose a path to an image file with =[[<path_to_image>]]=.

To export org files to HTML use ~htmlize~
#+begin_src emacs-lisp
(use-package htmlize
  :straight t)
#+end_src

I want to use Nicolas Rougier's style sheet for exporting org files to HTML, so here's a custom function for that:
#+begin_src emacs-lisp
(defun de/my-org-inline-css-hook (exporter)
  "Insert custom inline css"
  (when (eq exporter 'html)
    (let* ((dir (ignore-errors (file-name-directory (buffer-file-name))))
           (path (concat dir "style.css"))
           (homestyle (or (null dir) (null (file-exists-p path))))
           (final (if homestyle "~/Apps/emacs-config/custom/notebook.css" path))) ;; <- set your own style file path
      (setq org-html-head-include-default-style nil)
      (setq org-html-head (concat
                           "<style type=\"text/css\">\n"
                           "<!--/*--><![CDATA[/*><!--*/\n"
                           (with-temp-buffer
                             (insert-file-contents final)
                             (buffer-string))
                           "/*]]>*/-->\n"
                           "</style>\n")))))

(add-hook 'org-export-before-processing-hook 'de/my-org-inline-css-hook)
#+end_src

Please note that at the moment, there doesn't seem to be a good way to include
an SVG file in the HTML export??

For convenience, let's just base64-encode images when exporting an org file to HTML
#+begin_src emacs-lisp
(defun org-org-html--format-image (source attributes info)
  (format "<img src=\"data:image/%s+xml;base64,%s\"%s />"
	      (or (file-name-extension source) "")
	      (base64-encode-string
	       (with-temp-buffer
             (insert-file-contents-literally source)
             (buffer-string)))
	      (file-name-nondirectory source)))
(advice-add #'org-html--format-image :override #'org-org-html--format-image)
#+end_src

To invoke LaTeX preview, =C-c C-x C-l= (danger! don't reverse to C-x C-c
.. because that would close a window!)

For citations, I want to use csl styles, so I'll need the ~citeproc~ package
#+begin_src emacs-lisp
(use-package citeproc
  :after org
  :defer t
  :straight t)
#+end_src

to use citeproc you need to add ~#+CSL_STYLE~ AND ~#+BIBLIOGRAPHY~ tags in the org
document pointing to the relevant files (CSL file and bibliography exported
from zotero or manually constructed).

invoke ~org-cite-insert~, select the citation from the mini-buffer. Note that a
single citation can cite more than one references. When done do =M-<return>=.

** org document export to PDF

The custom LaTeX template is stored in the ~texmf~ directory locally, to figure out where it is do:
#+begin_src zsh
kpsewhich -var-value TEXMFHOME
#+end_src

For example, I store org-notes-template.cls in ~$TEXMFHOME/tex/latex/org-notes-template/~

If you put new templates in there, update the LaTeX database by running ~texhash~.

#+begin_src emacs-lisp
(unless (boundp 'org-latex-classes)
  (setq org-latex-classes nil))

(add-to-list 'org-latex-classes
             '("org-notes-template"
               "\\documentclass[%C]{org-notes-template}"
               ("\\section{%s}" . "\\section*{%s}")
               ("\\subsection{%s}" . "\\subsection*{%s}")
               ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
               ("\\paragraph{%s}" . "\\paragraph*{%s}")
               ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))

#+end_src

To use the template you just put this at the top of your org document:
#+begin_example
#+LATEX_CLASS: org-notes-template
#+LATEX_CLASS_OPTIONS: [10pt]
#+LATEX_HEADER: \usepackage[inkscapelatex=false]{svg}
#+end_example
Use the svg package when exporting svg images or graphs into PDF.



* Personal customizations
:PROPERTIES:
:header-args: :tangle yes
:END:
All custom functions are preceded by the prefix ~de/~. All other custom functions that can be called interactively is placed under ~~/Apps/emacs-config/custom~.

When programming, I often want to move lines/regions up or down, bound to
=Super-<up>/<down>=.

#+begin_src emacs-lisp
(defun de/move-text-internal (arg)
  (cond
   ((and mark-active transient-mark-mode)
    (if (> (point) (mark))
        (exchange-point-and-mark))
    (let ((column (current-column))
          (text (delete-and-extract-region (point) (mark))))
      (forward-line arg)
      (move-to-column column t)
      (set-mark (point))
      (insert text)
      (exchange-point-and-mark)
      (setq deactivate-mark nil)))
   (t
    (let ((column (current-column)))
      (beginning-of-line)
      (when (or (> arg 0) (not (bobp)))
        (forward-line)
        (when (or (< arg 0) (not (eobp)))
          (transpose-lines arg))
        (forward-line -1))
      (move-to-column column t)))))

(defun de/move-text-up (arg)
  "Move region (if selected) or current line up by ARG lines."
  (interactive "*p")
  (de/move-text-internal (- (or arg 1))))

(defun de/move-text-down (arg)
  "Move region (if selected) or current line down by ARG lines."
  (interactive "*p")
  (de/move-text-internal (or arg 1)))

(global-set-key (kbd "s-<up>") 'de/move-text-up)
(global-set-key (kbd "s-<down>") 'de/move-text-down)

#+end_src

Resizing windows is counter intuitive, so I'd like up/down/left/right to resize the window in the concordant directions. Note that this may not work when in ~org-mode~ because that keybinding may be occupied with something else (I think for doing shift-selection).
#+begin_src emacs-lisp
(global-set-key (kbd "s-C-<left>") 'shrink-window-horizontally)
(global-set-key (kbd "s-C-<right>") 'enlarge-window-horizontally)
(global-set-key (kbd "s-C-<down>") 'shrink-window)
(global-set-key (kbd "s-C-<up>") 'enlarge-window)
#+end_src

Load my custom functions
#+begin_src emacs-lisp
(load "/Users/delnatan/Apps/emacs-config/custom/DE_fun01.el" t nil t)
#+end_src

#+begin_src emacs-lisp
;; add my custom programs/functions to load-path
(add-to-list 'load-path "~/Apps/emacs/de-tools")
;; for drawing protein diagrams
(require 'protein-diagrams)
;; load molecular biology stuff
(require 'molbio-utils)
#+end_src

** Molecular Biology work
[2025-11-30 Sun] moved to ~~/Apps/emacs/de-tools/molbio-utils.el~.
[2025-11-19 Wed] added

Some tasks in wet lab is repetitive (like setting up a PCR). I decided to use ~dynamic blocks~ to try out how this can make my life a bit easier.


* Org-agenda
:PROPERTIES:
:header-args: :tangle yes
:END:
I've started using org-mode for scheduling stuff. For now, I'll just keep any
agenda items in my main todo-list.

#+begin_src emacs-lisp
;; setup org-agenda keybinding to `C-c a`
(global-set-key (kbd "C-c a") 'org-agenda)

(setq org-agenda-files
      '("/Users/delnatan/Library/CloudStorage/Dropbox/org/todo.org"
  	    "/Users/delnatan/Documents/org/tasks.org"
  	    "/Users/delnatan/Documents/org/meetings.org"
  	    "/Users/delnatan/Documents/org/events.org"
  	    "/Users/delnatan/Work/org/schedules.org"))
#+end_src

A convenient aspect of the org calendar system is that you can do arithmetic very easily. When you invoke a calendar minibuffer (e.g. via =C-c != or =C-c .=) you can simply just do ~+4w~ to set a date 4-weeks from today.

The syntax for relative dates is:
+N unit or -N unit

Where:
    + or - : Specifies addition or subtraction from the current date/time.
    N      : Is the number of units.
    unit   : Is a letter for the time unit:
    
        h for hours (relative to current time)
        d for days (relative to today at midnight)
        w for weeks (relative to today at midnight)
        m for months (relative to today at midnight)
        y for years (relative to today at midnight)


* Org-capture setup
:PROPERTIES:
:header-args: :tangle yes
:END:

In the templates here's what each placeholder means:
~%?~ is where the cursor will be placed for you to start typing
~%i~ is the initial content
~%a~ is an automatic link to the location where you initiated the capture
~%U~ inserts a timestamp
~%T~ prompts you date and time. Simply enter the date first and click on the desired date in the minibuffer

To prompt for the entry in the minibuffer. use ~%^{prompt}~.
Enter time in a 24-hour format.

You can add priorities to each TODO or notes by adding a =[#1]= (a numeric value to each tag). For example:
=* TODO [#1] my TODO item.= 

Generally, the notes are meant to be refiled so that it would show up when you run =M-x notes-list=. However, the TODO items get added to the calendar.

#+begin_src emacs-lisp
;; set =C-c c= to do org-capture
(define-key global-map (kbd "C-c c") 'org-capture)

;; set templates
(setq org-capture-templates
      '(("t" "TODO" entry (file+headline "/Users/delnatan/Documents/org/tasks.org" "Tasks")
    	 "TODO %U %? \n  %i\n" :prepend t :empty-lines 1)
  	("n" "Note" entry (file+headline "/Users/delnatan/Documents/org/notes.org" "Notes")
  	 "* %^{TITLE} :NOTE:\n#+DATE: %<%Y-%m-%d %a>\n#+FILETAGS: note\n#+SUMMARY: %^{SUMMARY}\n#+ICON: material/notebook\n%?\n" :prepend t :empty-lines 1)
  	("m" "Meeting" entry (file+headline "/Users/delnatan/Documents/org/meetings.org" "Meetings")
  	 "* %? :MEETING:\n%^T\n- Location: %^{Location}\n- Participants: %^{Participants}\n- Agenda:\n  - %^{Agenda}\n" :prepend t :empty-lines 1)
  	("e" "Event" entry (file+headline "/Users/delnatan/Documents/org/events.org" "Events")
  	 "* %? :EVENT:\n%^T\n- Location: %^{Location}\n-  %i\n" :prepend t :empty-lines 1)))

(defun de/org-sort-entries-by-date ()
  "Sort entries in descending order by date"
  (when (or (string= (buffer-name) "events.org")
	    (string= (buffer-name) "meetings.org"))
    (org-sort-entries nil ?T)))

(add-hook 'org-capture-after-finalize-hook 'de/org-sort-entries-by-date)

;; configure refile targets
(setq org-refile-targets '((nil :maxlevel . 3)
  			   (org-agenda-files :maxlevel . 3)))
#+end_src

The workflow is such: do =C-c n= to capture a note in a new buffer, then save the file and do =C-c C-c= to complete the capture. Do =C-c C-k= to abort capture.

To refile the captured notes and todo lists, do =C-c C-w=, entries can be refiled to files listed in ~org-agenda-files~.


* Miscellaneous support
:PROPERTIES:
:header-args: :tangle yes
:END:

I use OpenSCAD for 3D printing, so editing the files in Emacs is nice
#+begin_src emacs-lisp
(use-package scad-mode
  :defer t
  :straight (scad-mode :type git :host github :repo "openscad/emacs-scad-mode"))
#+end_src

Sometimes I work with a lot of CSV files when doing data processing using Python
#+begin_src emacs-lisp
(use-package csv-mode
  :defer t
  :straight (csv-mode :type git :host github :repo "emacsmirror/csv-mode"))
#+end_src

For reading pdfs, let's try out pdf-tools, which has a nice 'midnight' mode.
#+begin_src emacs-lisp 
(use-package pdf-tools
  :straight (pdf-tools :type git :host github :repo "vedang/pdf-tools")
  :config
  (pdf-tools-install :no-query)
  (setq-default pdf-view-display-size 'fit-width)
  (setq pdf-view-midnight-colors '("#e5e9f0" . "#2e3440")))
#+end_src

I started learning TypeScript. Note regarding the Emacs-style regex. ~\\.~ is to match a literal dot in the file name. You need to declare and escape and then ~\.~ to match a dot (because a dot matches any character in regex). The ~\\'~ just means the 'end of string'.

#+begin_src emacs-lisp
(add-to-list 'auto-mode-alist '("\\.js\\'" . js-ts-mode))
(add-to-list 'auto-mode-alist '("\\.ts\\'" . typescript-ts-mode))
(add-to-list 'auto-mode-alist '("\\.[jt]sx\\'" . tsx-ts-mode))
(add-to-list 'major-mode-remap-alist '(javascript-mode . js-ts-mode))
(add-to-list 'major-mode-remap-alist '(typescript-mode . typescript-ts-mode))
#+end_src

